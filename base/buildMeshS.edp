//-------------------------------------------------------
// LOAD & INCLUDES

// PACKAGE LOADING
load "msh3"
include	"getARGV.idp"
include "object.idp"

/* 
INCLUDE CONFIG FILE WITH FOLLOWING STUCTURE AS CSV:
objectType,theta,positionX,positionY,positionZ,objectParameter1,objectParameter2,objectParameter3,objectParameter4
*/
string configFile = getARGV("-c", "config.csv");

string meshSFile = getARGV("-o", "mesh.meshS");

icoSphereDir = getARGV("-i", "icosphere");

string settingFile = getARGV("-p", "config_setting.csv");

//-------------------------------------------------------
// SETTINGS

string[int] settings = readConfig(settingFile); 
assert(!checkIsEmpty(settings));

string boxSize = settings[0];
real boxSizeX = atof(getLineByIndex(boxSize, 0));
real boxSizeY = atof(getLineByIndex(boxSize, 1));
real boxSizeZ = atof(getLineByIndex(boxSize, 2));

string numElem = settings[1];
int numElemX = atoi(getLineByIndex(numElem, 0));
int numElemY = atoi(getLineByIndex(numElem, 1));
int numElemZ = atoi(getLineByIndex(numElem, 2));

//-------------------------------------------------------
// SET BOX */
meshS ThS;
{
    /* ----- BOX ----- */

    /* number of elements */
    int[int] dimBoxNumElem = [numElemX, numElemY, numElemZ];

    /* box (size-)dimension */
    real [int,int] B = [[-boxSizeX/2., boxSizeX/2.], 
                        [-boxSizeY/2., boxSizeY/2.], 
                        [-boxSizeZ/2., boxSizeZ/2.]];
    cout << "B " << B << endl;
    /* labels */
    int [int,int] L = [[1, 2], [3, 4], [5, 6]];

    ThS = SurfaceHex(dimBoxNumElem, B, L, 1);
}

//-------------------------------------------------------
// SET OBJECTS */

// LOAD FILES FROM CONFIG
string[int] config = readConfig(configFile); 
assert(!checkIsEmpty(config));

int numObjects = config.n;
meshS[int] ThSObjects(numObjects);
for( int ind = 0; ind < numObjects; ind++ ){
    
    string line = config[ind];
    // ---------------------------------

    string type = getType(line);
    int theta = getTheta(line);
    real[int] position = getPosition(line);
    real[int] parameters = getParameter(line);
    // ---------------------------------

    meshS ThSObject = loadMeshS( type, parameters );
    shiftMeshS( ThSObject, position, ind );
    formMeshS( ThSObject, type, position, parameters );
    // ---------------------------------

    ThSObjects[ind] = ThSObject;
    if( (ind + 1) % int(ceil(numObjects/10.0)) == 0 ){
        cout << "Loaded " << ind + 1 << " of " << numObjects << " objects." << endl;
    }
}

// MERGE OBJECTS
int cnt = 0;
while(cnt < numObjects){
    meshS temp500;
    int cnt500 = numObjects - cnt > 500 ? 500 : numObjects - cnt;
    for( int ind = 0; ind < cnt500; ind++ ){
        temp500 = ThSObjects[cnt] + temp500;
        cnt++;
        if( (cnt + 1) % int(ceil(numObjects/10.0)) == 0 ){
            cout << "Stored " << cnt  << " of " << numObjects << " objects." << endl;
        }
    }
    ThS = temp500 + ThS;
}




// SAVE MESH
savemesh( ThS, meshSFile );